apply plugin: 'base'

allprojects {
    apply plugin: 'java'
}

subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()

        maven {
            name 'spigot-repo'
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
        }

        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    dependencies {
        compile 'org.spigotmc:spigot-api:1.10.2-R0.1-SNAPSHOT'
    }
}

project(':Core') {
    dependencies {
        compile files('libs/spigot.jar')
    }
}

project(':Test') {
    dependencies {
        compile project(':Core')
    }
}

clean {
    delete 'server'
}

task copyServer(type: Copy) {
    from "${projectDir}/libs"
    into "server/"
}

task copyCorePlugin(type: Copy, dependsOn: [':Core:build']) {
    from project(':Core').buildDir.getAbsolutePath() + "/libs"
    into 'server/plugins/'
}

task copyTestPlugin(type: Copy, dependsOn: [':Test:build']) {
    from project(':Test').buildDir.getAbsolutePath() + "/libs"
    into 'server/plugins/Devathon-Plugin-Submission/scripts/'
}

task run(type: JavaExec, dependsOn: [':copyServer', ':copyCorePlugin', ':copyTestPlugin']) {
    doFirst {
        standardInput = System.in
        standardOutput = System.out
    }

    main = "org.bukkit.craftbukkit.Main"
    classpath "server/spigot.jar"
    systemProperties = ['com.mojang.eula.agree': 'true']
    args('--world-dir', './worlds/')
    workingDir = "server/"
}

gradle.taskGraph.whenReady { graph ->
    for (Task t : graph.getAllTasks()) {
        if (t.getProject() == rootProject) {
            continue
        }

        if (t.getName().equals("run")) {
            t.deleteAllActions()
        }
    }
}